
/////////////////////////////////////////////////////////////////////////////////////////// 
//			ОБРАБОТЧИКИ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НачалоПериода = Параметры.НачалоПериода;
	КонецПериода  = Параметры.КонецПериода;
	Если НачалоДня(НачалоПериода) = НачалоДня(КонецПериода) Тогда
		Календарь = НачалоПериода;
	Иначе
		Календарь = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		ЭтаФорма.Заголовок = СокрЛП(Параметры.Заголовок);
	КонецЕсли; 
	
	ТекущийРежим = Метаданные.РежимСовместимостиИнтерфейса;
	Если ТекущийРежим = Метаданные.СвойстваОбъектов.РежимСовместимостиИнтерфейса.Такси
		ИЛИ ТекущийРежим = Метаданные.СвойстваОбъектов.РежимСовместимостиИнтерфейса.ТаксиРазрешитьВерсия8_2 Тогда
		ЦветФонаКнопкиВыбранногоПериода = ЦветаСтиля.ФонПомеченнойКнопкиЦвет;
	Иначе
		ЦветФонаКнопкиВыбранногоПериода = Новый Цвет(235, 231, 205);
		Элементы.Вспом1.Высота = 2;
	КонецЕсли; 
	ЦветФонаКнопки = ЦветаСтиля.ЦветФонаКнопки;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	Если НЕ ЗначениеЗаполнено(НачалоПериода) и НЕ ЗначениеЗаполнено(КонецПериода) Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Дата не выбрана.'"));
		Возврат;
	КонецЕсли; 
	
	ДействиеВыбрано = Истина;
	РезультатВыбора = Новый Структура("НачалоПериода, КонецПериода", НачалоДня(НачалоПериода), КонецДня(КонецПериода));		
	ОповеститьОВыборе(РезультатВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура КалендарьПриИзменении(Элемент)
	ВыделенныеДаты = Элементы.Календарь.ВыделенныеДаты;
	Элементы.СНачалаГода.Доступность = ВыделенныеДаты.Количество() <= 1;
	Если НЕ Элементы.СНачалаГода.Доступность Тогда
		СНачалаГода = Ложь;
	КонецЕсли; 
	
	УстановитьВыбираемыйПериод("ИНТЕРВАЛ");
КонецПроцедуры

&НаКлиенте
Процедура КалендарьПриВыводеПериода(Элемент, ОформлениеПериода)
	Календарь = ОформлениеПериода.КонецПериода;
КонецПроцедуры

&НаКлиенте
Процедура СНачалаГодаПриИзменении(Элемент)
	УстановитьВыбираемыйПериод(РежимВыбораПериода);
КонецПроцедуры

#КонецОбласти

/////////////////////////////////////////////////////////////////////////////////////////// 
//			ОБРАБОТКА ФИКСИРОВАННЫХ ПЕРИОДОВ

#Область ОбработкаФиксированныхПериодов

&НаКлиенте
Процедура ВыборМесяц(Команда)
	Элементы.СНачалаГода.Доступность = Истина;	
	ЧислоМесяца = Число(Сред(Команда.Имя,6));
	Календарь = Дата(Год(Календарь), ЧислоМесяца, 1);
	УстановитьВыбираемыйПериод("МЕСЯЦ");
КонецПроцедуры

&НаКлиенте
Процедура ВыборКвартала(Команда)
	Элементы.СНачалаГода.Доступность = Истина;	
	ЧислоКвартала = Число(Сред(Команда.Имя,8));
	НачальныйМесяцКвартала = 3 * (ЧислоКвартала - 1) + 1;
	Календарь = Дата(Год(Календарь), НачальныйМесяцКвартала, 1);
	УстановитьВыбираемыйПериод("КВАРТАЛ");
КонецПроцедуры

&НаКлиенте
Процедура КомандаГод(Команда)
	Элементы.СНачалаГода.Доступность = Ложь;
	СНачалаГода = Ложь;
	Календарь = Дата(Год(Календарь), 1, 1);
	УстановитьВыбираемыйПериод("ГОД");
КонецПроцедуры

#КонецОбласти


/////////////////////////////////////////////////////////////////////////////////////////// 
//			СЛУЖЕБНЫЕ

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СнятьВыделенныеСтандартныеПериоды()
	
	Для НомерМесяца = 1 По 12 Цикл
		КнопкаМесяца = Элементы["Месяц" + Формат(НомерМесяца, "ЧЦ=2; ЧВН=")];
		КнопкаМесяца.ЦветФона = ЦветФонаКнопки;
	КонецЦикла;
	
	Для НомерКвартала = 1 По 4 Цикл
		КнопкаМесяца = Элементы["Квартал" + Формат(НомерКвартала, "ЧЦ=1")];
		КнопкаМесяца.ЦветФона = ЦветФонаКнопки;
	КонецЦикла;

	Элементы.КомандаГод.ЦветФона = ЦветФонаКнопки;	
	
КонецПроцедуры // СнятьВыделенныеСтандартныеПериоды()

&НаКлиенте
Процедура ОбновитьОтображение(РежимПериода)
	
	ВыбираемыйПериод = НачалоДня(НачалоПериода);
	СнятьВыделенныеСтандартныеПериоды();
	ВыделенныеДаты = Элементы.Календарь.ВыделенныеДаты;
	
	Если РежимПериода = "МЕСЯЦ" Тогда
		НомерМесяца = Месяц(ВыбираемыйПериод);
		КнопкаМесяца = Элементы["Месяц" + Формат(НомерМесяца, "ЧЦ=2; ЧВН=")];
		КнопкаМесяца.ЦветФона = ЦветФонаКнопкиВыбранногоПериода;
		ЭтаФорма.ТекущийЭлемент = КнопкаМесяца;
		
		Элементы.ПериодСтрокой.Заголовок = ?(СНачалаГода И НомерМесяца <> 1, "Январь - ", "") + Формат(ВыбираемыйПериод, "ДФ='ММММ гггг'");
		ВыделенныеДаты.Очистить();
		ВыделенныеДаты.Добавить(КонецПериода);
		Элементы.Календарь.КонецПериодаОтображения = КонецПериода;
		
	ИначеЕсли РежимПериода = "КВАРТАЛ" Тогда
		НомерКвартала = (Месяц(ВыбираемыйПериод) - 1) / 3 + 1;
		КнопкаКвартала = Элементы["Квартал" + Формат(НомерКвартала, "ЧЦ=1")];
		КнопкаКвартала.ЦветФона = ЦветФонаКнопкиВыбранногоПериода;
		ЭтаФорма.ТекущийЭлемент = КнопкаКвартала;
		
		Элементы.ПериодСтрокой.Заголовок = ?(СНачалаГода И НомерКвартала <> 1, "I квартал - ", "") + КнопкаКвартала.Заголовок + " " + Формат(Календарь,"ДФ=гггг");
		ВыделенныеДаты.Очистить();
		ВыделенныеДаты.Добавить(КонецПериода);
		
	ИначеЕсли РежимПериода = "ГОД" Тогда
		Элементы.КомандаГод.ЦветФона = ЦветФонаКнопкиВыбранногоПериода;
		ЭтаФорма.ТекущийЭлемент = Элементы.КомандаГод;
		
		Элементы.ПериодСтрокой.Заголовок = Формат(Календарь,"ДФ=гггг") + " год";
		ВыделенныеДаты.Очистить();
		ВыделенныеДаты.Добавить(КонецПериода);
	Иначе
		ЭтаФорма.ТекущийЭлемент = Элементы.Календарь;
		Элементы.ПериодСтрокой.Заголовок = ПредставлениеПериода(?(СНачалаГода, НачалоГода(Календарь), НачалоПериода), КонецПериода, "ФП = Истина");	
	КонецЕсли;
	
	Элементы.Календарь.НачалоПериодаОтображения = НачалоМесяца(КонецПериода);
	Элементы.Календарь.КонецПериодаОтображения = КонецПериода;	
	Элементы.Календарь.Обновить();  
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВыбираемыйПериод(РежимПериода)
	
	Если РежимПериода = "МЕСЯЦ" Тогда
		НачалоПериода	= НачалоДня(Календарь);
		КонецПериода	= КонецМесяца(Календарь);
		РежимВыбораПериода = "МЕСЯЦ";
		
	ИначеЕсли РежимПериода = "КВАРТАЛ" Тогда
		НачалоПериода 	= НачалоКвартала(Календарь);
		КонецПериода	= КонецКвартала(Календарь);
	ИначеЕсли РежимПериода = "ГОД" Тогда
		НачалоПериода 	= НачалоГода(Календарь);
		КонецПериода	= КонецГода(Календарь);
		
	Иначе
		ВыделенныеДаты = Элементы.Календарь.ВыделенныеДаты;	
		Если ВыделенныеДаты.Количество() = 0 Тогда
			Элементы.ПериодСтрокой.Заголовок = "";		
			Возврат;
		ИначеЕсли ВыделенныеДаты.Количество() = 1 Тогда
			// выделен один день
			Календарь = ВыделенныеДаты[0];
			НачалоПериода 	= НачалоДня(Календарь);
			КонецПериода	= КонецДня(Календарь);
		Иначе
			ПерваяДата 		= ВыделенныеДаты[0];
			ПоследняяДата 	= ВыделенныеДаты[ВыделенныеДаты.Количество() - 1];
			Календарь 		= ?(ПерваяДата < ПоследняяДата, ПерваяДата, ПоследняяДата);
			СНачалаГода 	= Ложь;
			НачалоПериода 	= НачалоДня(Календарь);
			КонецПериода	= КонецДня(?(ПерваяДата < ПоследняяДата, ПоследняяДата, ПерваяДата));
		КонецЕсли; 
	КонецЕсли; 
	
	ОбновитьОтображение(РежимПериода);
	НачалоПериода = ?(СНачалаГода, НачалоГода(Календарь), НачалоПериода);
	Элементы.Календарь.НачалоПериодаОтображения 	= Дата(1,1,1);	
	Элементы.Календарь.КонецПериодаОтображения 		= Дата(1,1,1);
	РежимВыбораПериода = РежимПериода;	
	
КонецПроцедуры

#КонецОбласти

