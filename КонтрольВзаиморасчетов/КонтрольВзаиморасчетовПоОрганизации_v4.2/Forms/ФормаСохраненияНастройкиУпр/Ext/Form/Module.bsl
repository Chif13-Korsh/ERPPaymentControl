
/////////////////////////////////////////////////////////////////////////////////////////// 

#Область СобытияФормы
	
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ПараметрыОткрытияФормыОтчета", ПараметрыОткрытияФормыОтчета) Тогда
		Возврат;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Параметры.НастройкиОтчета) Тогда
		Возврат;
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Параметры.ТекущийВариант) Тогда
		Наименование = Параметры.ТекущийВариант.Наименование;
		Для каждого Эл Из Параметры.ТекущийВариант.Метаданные().Реквизиты Цикл
			Если Эл.Имя = "Пользовательский" Тогда
				ЭтотОбъект.БыстрыйДоступ = Параметры.ТекущийВариант.Пользовательский;
			ИначеЕсли Эл.Имя = "Настройки" Тогда
				НастройкаПользователя = Параметры.ТекущийВариант.Настройки.Получить();
				Отчет.КомпоновщикНастроек.Восстановить();
				Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(НастройкаПользователя);
			Иначе
				Попытка
					ЭтотОбъект[Эл.Имя] = Параметры.ТекущийВариант[Эл.Имя];
				Исключение КонецПопытки; 
			КонецЕсли; 
		КонецЦикла; 
	Иначе
		// значит новый
		Автор = ПараметрыСеанса.ТекущийПользователь;
		ТолькоДляАвтора = Истина;
		Наименование = Параметры.НастройкиОтчета.ПредставлениеВарианта;
	КонецЕсли; 
	
	Доступен = ?(ТолькоДляАвтора, "1", "2");
	
	ГлУзелОтчетов 	= Параметры.НастройкиОтчета.ГлУзелВариантовОтчета;
	КлючВарианта 	= Параметры.КлючВарианта;
	ИмяОтчета 		= Параметры.ИмяОтчета;
	
	ПолныеПраваНаВарианты = ВариантыОтчетов.ПолныеПраваНаВарианты();
	ПравоНаЭтотВариант = ПолныеПраваНаВарианты Или Параметры.ТекущийВариант.Автор = Пользователи.АвторизованныйПользователь();
	Если Не ПравоНаЭтотВариант Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Элементы.Доступен.ТолькоПросмотр = Не ПолныеПраваНаВарианты;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ПараметрыОткрытияФормыОтчета <> Неопределено Тогда
		Отказ = Истина;
		ВариантыОтчетовКлиент.ОткрытьФормуОтчета(Неопределено, ПараметрыОткрытияФормыОтчета);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ПараметрОповещения = Новый Структура("Ссылка, Наименование, Автор, Описание");
	ЗаполнитьЗначенияСвойств(ПараметрОповещения, Отчет);
	Оповестить(ВариантыОтчетовКлиентСервер.ИмяСобытияИзменениеВарианта(), ПараметрОповещения, ЭтотОбъект);
	СтандартныеПодсистемыКлиент.РазвернутьУзлыДерева(ЭтотОбъект, "ДеревоПодсистем", "*", Истина);
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОписаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ВариантыОтчетовКлиент.РедактироватьМногострочныйТекст(ЭтотОбъект, Элемент.ТекстРедактирования, Отчет, "Описание", НСтр("ru = 'Описание'"));
КонецПроцедуры

&НаКлиенте
Процедура ДоступенПриИзменении(Элемент)
	ТолькоДляАвтора = (ЭтотОбъект.Доступен = "1");
КонецПроцедуры

&НаКлиенте
Процедура БыстрыйДоступНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	БыстрыйДоступ = Не БыстрыйДоступ;
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаСервере
Функция СохранитьНастройкиСервер(Заменить = Ложь, СтруктураПараметров = Неопределено)
	Если Заменить Тогда
		Если ЗначениеЗаполнено(Параметры.ТекущийВариант) Тогда
			ВариантОбъект = Параметры.ТекущийВариант.ПолучитьОбъект();
		ИначеЕсли ТипЗнч(СтруктураПараметров) = Тип("Структура") И ЗначениеЗаполнено(СтруктураПараметров.НайденныйВариант) Тогда
			ВариантОбъект = СтруктураПараметров.НайденныйВариант.ПолучитьОбъект();
		КонецЕсли; 
	Иначе
		ВариантОбъект = Справочники.ВариантыОтчетов.СоздатьЭлемент();
		ВариантОбъект.ТипОтчета = ПредопределенноеЗначение("Перечисление.ТипыОтчетов.Дополнительный");
	КонецЕсли;
		
	ВариантОбъект.Наименование 			= Наименование;
	ВариантОбъект.Отчет					= ИмяОтчета;
	ВариантОбъект.Родитель				= ГлУзелОтчетов;
	ВариантОбъект.КлючВарианта			= КлючВарианта;
	ВариантОбъект.Автор 				= Автор;
	ВариантОбъект.Пользовательский 		= БыстрыйДоступ;   // быстрый доступ
	ВариантОбъект.ВидимостьПоУмолчанию 	= Ложь;
	ВариантОбъект.Описание 				= Описание;
	ВариантОбъект.ТолькоДляАвтора 		= ТолькоДляАвтора;
	
	Настройки = ПолучитьИзВременногоХранилища(Параметры.НастройкиОтчета.АдресНастроекКомпановщика);
	// вернем настройкам заголовок без периодов
	Титл = Настройки.ПараметрыВывода.Элементы.Найти("Title");
	Титл.Значение = СокрЛП(Параметры.НастройкиОтчета.ПредставлениеВарианта);
	
	ВариантОбъект.Настройки = Новый ХранилищеЗначения(Настройки, Новый СжатиеДанных(9));
	ВариантОбъект.Записать();
	
	Возврат ВариантОбъект.Ссылка;
	
КонецФункции // СохранитьНастройкиСервер()

&НаКлиенте
Процедура ПолученОтветНаВопросОСохранении(Результат, ПараметрыВопроса) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		НоваяНастройка = СохранитьНастройкиСервер(Истина, ПараметрыВопроса);
		Закрыть(НоваяНастройка);		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		НоваяНастройка = СохранитьНастройкиСервер(Ложь);
		Закрыть(НоваяНастройка);		
	Иначе
	    Возврат;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройку(Команда)
	Перем НайденныйВариант;
	
	Если НЕ ЗначениеЗаполнено(Наименование) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Необходимо заполнить наименование настройки!";
		Сообщение.Поле = "Наименование";
		Сообщение.Сообщить(); 
		Возврат;
	КонецЕсли;
	
	ЕстьИмя = ПроверитьНаименованиеНовогоВарианта(НайденныйВариант);
	
	Если ЕстьИмя Тогда
		ВопросСохранениеНастроек = Новый ОписаниеОповещения("ПолученОтветНаВопросОСохранении", ЭтотОбъект, Новый Структура("НайденныйВариант",НайденныйВариант));
		ПоказатьВопрос(ВопросСохранениеНастроек, "Заменить настройку отчета?", РежимДиалогаВопрос.ДаНетОтмена,10,,"Сохранение настройки");
	Иначе
		НоваяНастройка = СохранитьНастройкиСервер();
		Закрыть(НоваяНастройка);
	КонецЕсли;
КонецПроцедуры

// ---------------------------------------------

&НаСервере
Процедура ВыгрузитьНастройкуНаСервере(ПутьКФайлу)
	Настройки  = Отчет.КомпоновщикНастроек.Настройки;
	НовыйСXDTO = Новый СериализаторXDTO(ФабрикаXDTO);

	Запись = Новый ЗаписьXML;	
	Запись.ОткрытьФайл(ПутьКФайлу);
	Запись.ЗаписатьОбъявлениеXML();
	
	НовыйСXDTO.ЗаписатьXML(Запись, Настройки, НазначениеТипаXML.Явное, ФормаXML.Элемент);
	
	Запись.Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьНастройку(Команда)
	ОбработатьНачалоВыбораФайла(Истина);
	Если ЗначениеЗаполнено(ПутьКФайлу) Тогда
		ВыгрузитьНастройкуНаСервере(ПутьКФайлу);		
	КонецЕсли; 
КонецПроцедуры


&НаСервере
Процедура ЗагрузитьНастройкуНаСервере(ПутьКФайлу)
	Чтение = Новый ЧтениеXML;
	НовыйСXDTO = Новый СериализаторXDTO(ФабрикаXDTO);	
	Попытка
		Чтение.ОткрытьФайл(ПутьКФайлу);
		Настройки = НовыйСXDTO.ПрочитатьXML(Чтение);
		Отчет.КомпоновщикНастроек.Восстановить();
		Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	Исключение Возврат КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаОЗагрузкиНастройки(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;
	
	ОбработатьНачалоВыбораФайла(Ложь);
	Если ЗначениеЗаполнено(ПутьКФайлу) Тогда
		ЗагрузитьНастройкуНаСервере(ПутьКФайлу);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОтветаОЗагрузкиНастройки()

&НаКлиенте
Процедура ЗагрузитьНастройку(Команда)
	ОповещениеОтвета = Новый ОписаниеОповещения("ОбработкаОтветаОЗагрузкиНастройки", ЭтотОбъект);  
	ПоказатьВопрос(ОповещениеОтвета, "Загруженные настройки заменят данные, продолжить?", РежимДиалогаВопрос.ДаНет); 
КонецПроцедуры

#КонецОбласти 

#Область Вспомогательные

&НаКлиенте
Процедура ОбработатьНачалоВыбораФайла(РежимВыгрузки = Истина)
	
	Если Не ПодключитьРасширениеРаботыСФайлами() Тогда
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ЗавершениеУстановкиРасширения", ЭтаФорма);		
		НачатьУстановкуРасширенияРаботыСФайлами(ОписаниеОповещенияОЗавершении);
	КонецЕсли;
	
	РежимДиалога = ?(РежимВыгрузки, РежимДиалогаВыбораФайла.Сохранение, РежимДиалогаВыбораФайла.Открытие);
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалога);
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Не РежимВыгрузки;
	ДиалогВыбораФайла.ПредварительныйПросмотр     = Ложь;	
	ДиалогВыбораФайла.МножественныйВыбор = Ложь;
	ДиалогВыбораФайла.Каталог = ПутьКФайлу;
	ДиалогВыбораФайла.Заголовок = Нстр("ru = 'Выберите файл для " + ?(РежимВыгрузки, "загрузки'", "выгрузки'"));
	ДиалогВыбораФайла.ПолноеИмяФайла = ?(ЗначениеЗаполнено(Наименование) И РежимВыгрузки, СокрЛП(Наименование) + ".xml", ПутьКФайлу);
	
	ДиалогВыбораФайла.Фильтр = "Формат выгрузки(*.xml)|*.xml|Все файлы (*.*)|*.*";
	ПутьКФайлу = "";
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		ПутьКФайлу = ДиалогВыбораФайла.ПолноеИмяФайла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьНаименованиеНовогоВарианта(НайденныйВариант)
	Возврат РеквизитФормыВЗначение("Отчет").ПроверитьНаименованиеВариантаОтчетаНаДубль(Наименование, Параметры.НастройкиОтчета, НайденныйВариант);
КонецФункции // ПроверитьНаименованиеНовогоВарианта()
 
#КонецОбласти 